grammar dsl;

startRule: directive* EOF;

directive: component | network | constraint | strategy | constant;

// ---- CONSTANT -----

constant: 'const' ID '=' expr ';';

// ---- STRATEGY -----

strategy: 'strategy' ID '{' strategy_property* '}';

strategy_property: strategy_foreach | strategy_call;

strategy_foreach: 'for' ID 'in' expr '{' strategy_property* '}';

strategy_call: access '('  ')' ';' ;

// ---- CONSTRAINT -----

constraint: 'constraint' ID '{' constraint_property* '}';

constraint_property: constraint_foreach | constraint_require | constraint_if;

constraint_foreach: 'for' ID 'in' expr '{' constraint_property* '}';
constraint_if:      'if' expr '{' constraint_property* '}';
constraint_require: 'require' '[' expr ']' expr ';';

// ---- CONNECTION -----

network: 'network' object_name '{' connection* '}';

connection: access ('->' access)+ ';' ;


// ---- COMPONENT -----

object_name: ID ('[' ID ':' ID ']')?
    ;
        
component: 'component' object_name '{' component_property*  '}';

component_property:
        'dimensions' '{' dim_prop* '}'
    |   'datasheet'  '{' datasheet_prop* '}'
    |   location_prop
    |   'pin'  pin_name   '{' pin_prop* '}'
    |   'value' ':' value=expr ';'
    |   'type'  ':' component_type=STRING ';'
    ;

pin_name: ID (',' ID)* ;

pin_prop: (pinmode='input' | pinmode='output' | pinmode='digital_ground' | pinmode='analog_ground') ';';

location_prop:
        ('at' ':' x=expr ',' y=expr ('layer' layer=NUMBER)? )?
	(rotatable='rotatable')?	
	 ';'
    ;

datasheet_prop:
        'extract' ':' extractor=ID '(' title=STRING ')' 'as' table=ID ';'
    |   'file'    ':' filename=STRING ';'
    ;

dim_prop: 'width'  ':' width=expr ';'
    |     'height' ':' height=expr ';'
    |     'layers' ':' layers=expr ';'
    ;


// ---- EXPR -----

primary:
        n=NUMBER unit
    |   '(' expr ')'
    |   access
    |   length='#' ID
    |   len
    ;

len : 'len' '(' access '->' access ')';
        

expr: primary (op primary)?
    ;

op:   operand='==' | operand='!= '
    | operand='<'  | operand='>'
    | operand='<=' | operand='>='
    | operand='+'  | operand='-' | operand='*' | operand='/'
    ;

access: ID index? access_suffix*
    ;

access_suffix: ('.' ID index?)
    ;	   

index: '[' ID ']';


unit: name='cm'
    | name='mm'
    | name='layer'
    | name='ohm'
    | name='uF'
    | name='integer'
    ;

// ---- TOKENS ----

STRING
   : '"' ~ ["\r\n]* '"'
   ;

ID: [a-zA-Z][_a-zA-Z0-9]*;

NUMBER: [0-9]+ ('.' [0-9]+)?;

WS: [ \r\n\t]+ -> skip
   ;


COMMENT: '/*' .*? '*/' -> skip
    ;

LINE_COMMENT: '//' ~[\r\n]* -> skip
    ;
